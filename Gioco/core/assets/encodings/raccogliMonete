%casella(X,Y).
%moneta(X,Y,Tipo).
%pacman(X,Y).
%monetaVicina(X,Y). aggiungere controllo per avvicinarsi alla più vicina

%Guess sulle mosee possibili
up(T)|down(T)|right(T)|left(T):-pacmanDLV(T,X,Y),T<7.

%vincolo mossa consentita
:- up(T),pacmanDLV(T,X,Y),not casella(X-1,Y).
:- down(T),pacmanDLV(T,X,Y),not casella(X+1,Y).
:- left(T),pacmanDLV(T,X,Y),not casella(X,Y-1).
:- right(T),pacmanDLV(T,X,Y),not casella(X,Y+1).

%aggiorna posizione pacman
pacmanDLV(T+1,X-1,Y):-up(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X+1,Y):-down(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X,Y-1):-left(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X,Y+1):-right(T),pacmanDLV(T,X,Y).

raccoltaMonetaNormale(T,X,Y):- pacmanDLV(T,X,Y),moneta(X,Y,"normale").
raccoltaMonetaNormale(T+1,X,Y):-raccoltaMonetaNormale(T,X,Y),T<7.

:~ pacmanDLV(T,X,Y), not moneta(X,Y,"normale"). [1@4,T,X,Y]
:~ pacmanDLV(T,X,Y), raccoltaMonetaNormale(T-1,X,Y). [1@4,T,X,Y]

doveviRaccogliere(T,X1,Y1):- pacmanDLV(T,X1,Y1),#count{X,Y : moneta(X,Y,"normale"),not raccoltaMonetaNormale(T-1,X,Y),X>=X1-1,Y>=Y1-1,X<=X1+1,Y<=Y1+1}=0, not moneta(X1,Y1,"speciale"). 
:~doveviRaccogliere(T,X1,Y1). [1@3,T,X1,Y1] 
nonDoveviRaccogliere(T,X1,Y1):- pacmanDLV(T,X1,Y1),#count{X,Y : moneta(X,Y,"normale"),not raccoltaMonetaNormale(T-1,X,Y),X>=X1-1,Y>=Y1-1,X<=X1+1,Y<=Y1+1}>0, moneta(X1,Y1,"speciale"). 
:~nonDoveviRaccogliere(T,X1,Y1).[1@3,T,X1,Y1] 

distanza(D) :- monetaPiuVicina(X1,Y1),pacmanDLV(7,X2,Y2),X2>=X1,Y2>=Y1,D1=X2-X1,D2=Y2-Y1,D=D1+D2.
distanza(D) :- monetaPiuVicina(X1,Y1),pacmanDLV(7,X2,Y2),X2>=X1,Y2<=Y1,D1=X2-X1,D2=Y1-Y2,D=D1+D2.
distanza(D) :- monetaPiuVicina(X1,Y1),pacmanDLV(7,X2,Y2),X2<=X1,Y2>=Y1,D1=X1-X2,D2=Y2-Y1,D=D1+D2.
distanza(D) :- monetaPiuVicina(X1,Y1),pacmanDLV(7,X2,Y2),X2<=X1,Y2<=Y1,D1=X1-X2,D2=Y1-Y2,D=D1+D2.

:~ #count{R,C : raccoltaMonetaNormale(7,R,C)}=0,distanza(D). [D@2]
% qualora non è riuscito fino al tempo 3 a raccogliere almeno una moneta, paga la distanza dalla moneta più vicina in modo da minimizzarla.
%:~ #count{R,C : raccoltaMonetaNormale(7,R,C)}=0, monetaPiuVicina(X1,Y1),pacmanDLV(7,X2,Y2),&distanza(X2,Y2,X1,Y1;D1). [D1@2]