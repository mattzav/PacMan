%casella(X,Y).
%moneta(X,Y,Tipo).
%pacman(X,Y).
%nemicoPiuVicino(X,Y).
%numeroMosseMassimo(X).

casella(1,1). casella(1,2). casella(1,3). casella(1,4). casella(1,5). casella(1,6).
casella(2,1). casella(2,2). casella(2,3). casella(2,4). casella(2,5). casella(2,6).
casella(3,1). casella(3,2).  casella(3,4). casella(3,5). casella(3,6).
casella(4,1). casella(4,2).  casella(4,4). casella(4,5). casella(4,6).
 casella(5,2). casella(5,3). casella(5,5). casella(5,6).
 casella(6,2). casella(6,3). casella(6,4). casella(6,5). casella(6,6).

numeroMosseMassimo(8).
moneta(3,4,normale).
moneta(5,3,normale).
moneta(2,3,speciale).

pacman(5,2).
nemicoPiuVicino(4,4).
%posizione iniziale
pacmanDLV(0,X,Y):-pacman(X,Y).

%Guess sulle mosee possibili
up(T)|down(T)|right(T)|left(T):-pacmanDLV(T,X,Y),numeroMosseMassimo(K),T<K,not raggiunto(T).

%vincolo nemico raggiunto
raggiuntoNemico :- pacmanDLV(_,X,Y),nemicoPiuVicino(X,Y).
:- not raggiuntoNemico.

%pago per quanto impiego a raggiungerlo
raggiunto(T) :- pacmanDLV(T,X,Y),nemicoPiuVicino(X,Y).
:~ raggiunto(T). [T@10]

%vincolo mossa consentita
:- up(T),pacmanDLV(T,X,Y),not casella(X-1,Y).
:- down(T),pacmanDLV(T,X,Y),not casella(X+1,Y).
:- left(T),pacmanDLV(T,X,Y),not casella(X,Y-1).
:- right(T),pacmanDLV(T,X,Y),not casella(X,Y+1).

%aggiorna posizione pacman
pacmanDLV(T+1,X-1,Y):-up(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X+1,Y):-down(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X,Y-1):-left(T),pacmanDLV(T,X,Y).
pacmanDLV(T+1,X,Y+1):-right(T),pacmanDLV(T,X,Y).

:~ pacmanDLV(T,X,Y), not moneta(X,Y,normale). [1@2,T,X,Y]
:~ pacmanDLV(T,X,Y), not moneta(X,Y,speciale). [1@1,T,X,Y]
